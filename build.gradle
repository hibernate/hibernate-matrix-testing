
plugins {
	id 'groovy'
	id 'java-gradle-plugin'
	id "com.gradle.plugin-publish" version "0.11.0"
	id 'idea'
	id 'eclipse'
}

// NOTE : plugins published to the Gradle Plugin Portal automatically
// have `gradle.plugin.` prepended to their group name.
//
// It does allow that to be overridden, but then publishing requires
// manual intervention from Gradle team to approve...  not worth the
// hassle
//
// So long story short, not putting "gradle" in groupName here

group = 'org.hibernate.build'
version = '3.0.0-SNAPSHOT'

defaultTasks 'test'

repositories {
	mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.apache.ant:ant:1.8.2'
}

gradlePlugin {
	plugins {
		profilesPlugin {
			id = 'org.hibernate.build.hibernate-matrix-testing'
			implementationClass = 'org.hibernate.build.gradle.testing.matrix.MatrixTestingPlugin'
		}
	}
}

pluginBundle {
	website = 'https://github.com/hibernate/hibernate-matrix-testing'
	vcsUrl = 'https://github.com/hibernate/hibernate-matrix-testing'
	tags = ['matrix','testing']

	plugins {
		profilesPlugin {
			id = 'org.hibernate.build.hibernate-matrix-testing'
			displayName = 'Gradle plugin for dynamic generation of Test tasks across a set of database profile'
			description = 'Gradle plugin for dynamic generation of Test tasks across a set of database profile'
		}
	}
}

task compile
tasks.compile.dependsOn tasks.compileJava, tasks.compileTestJava

idea {
	project {
		languageLevel = '1.8'
	}
	module {
		downloadSources = true
		downloadJavadoc = true
	}
}

task copyResourcesToIntelliJOutFolder(dependsOn: tasks.processTestResources) {
	doLast {
		copy {
			from "$buildDir/resources/test"
			into 'out/test/resources'
		}
	}
}

wrapper {
	// To upgrade the version of gradle used in the wrapper, run:
	//     ./gradlew wrapper --gradle-version NEW_VERSION
	distributionType = Wrapper.DistributionType.ALL
}
