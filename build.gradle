buildscript {
    repositories {
        mavenCentral()
        mavenLocal()

		maven {
			name 'jboss-nexus'
			url "https://repository.jboss.org/nexus/content/groups/public/"
		}
		maven {
			name "jboss-snapshots"
			url "http://snapshots.jboss.org/maven2/"
		}
    }
    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:3.0.0'
    }
}

repositories {
    mavenCentral()
    mavenLocal()

		maven {
			name 'jboss-nexus'
			url "https://repository.jboss.org/nexus/content/groups/public/"
		}
		maven {
			name "jboss-snapshots"
			url "https://snapshots.jboss.org/maven2/"
		}
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'

apply plugin: 'idea'
apply plugin: 'eclipse'

group = 'org.hibernate.build.gradle'
version = '3.0.0'

buildDir = "target"

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.apache.ant:ant:1.8.2'
}

idea {
	project {
		languageLevel = '1.8'
	}
	module {
		downloadSources = true
		downloadJavadoc = true
	}
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	archiveClassifier.set( 'sources' )
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar

            pom {
                name = 'Matrix Testing Plugin for Gradle'
                description = 'Gradle plugin for dynamic generation of Test tasks across a set of database profile'
                url = 'https://hibernate.org'
                organization {
                    name = 'Hibernate.org'
                    url = 'https://hibernate.org'
                }
                scm {
                    url = 'https://github.com/hibernate/hibernate-matrix-testing'
                    connection = 'scm:git:https://github.com/hibernate/hibernate-matrix-testing.git'
                    developerConnection = 'scm:git:git@github.com:hibernate/hibernate-matrix-testing.git'
                }
                licenses {
                    license {
                        name = 'GNU Lesser General Public License'
                        url = 'https://www.gnu.org/licenses/lgpl-2.1.html'
                        comments = 'See discussion at http://hibernate.org/license for more details.'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'hibernate-team'
                        name = 'The Hibernate Development Team'
                        organization = 'Hibernate.org'
                        organizationUrl = 'https://hibernate.org'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            if ( version.endsWith( 'SNAPSHOT' ) ) {
                name 'jboss-snapshots-repository'
                url 'https://repository.jboss.org/nexus/content/repositories/snapshots'
            }
            else {
                name 'jboss-releases-repository'
                url 'https://repository.jboss.org/nexus/service/local/staging/deploy/maven2/'
            }
        }
    }
}

wrapper {
	// To upgrade the version of gradle used in the wrapper, run:
	//     ./gradlew wrapper --gradle-version NEW_VERSION
	distributionType = Wrapper.DistributionType.ALL
}
