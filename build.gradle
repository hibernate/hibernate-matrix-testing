
plugins {
	id 'java'
	id 'groovy'

    id 'java-gradle-plugin'

    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'nu.studer.credentials' version '2.1'

	id 'idea'
	id 'eclipse'
}

repositories {
    mavenCentral()
}

group = 'org.hibernate.build.gradle'
version = '3.0.1-SNAPSHOT'

buildDir = "target"

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.apache.ant:ant:1.8.2'
}

gradlePlugin {
    plugins {
        matrixPlugin {
            id = 'org.hibernate.matrix'
            implementationClass = 'org.hibernate.build.gradle.testing.matrix.MatrixTestingPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/hibernate/hibernate-orm/tree/main/tooling/hibernate-gradle-plugin'
    vcsUrl = 'https://github.com/hibernate/hibernate-orm/tree/main/tooling/hibernate-gradle-plugin'
    tags = ['hibernate','orm','bytecode','enhancement','bytebuddy']

    mavenCoordinates {
        groupId = project.group.toString()
        artifactId = project.name
        version = project.version.toString()
    }

    plugins {
        matrixPlugin {
//            id = project.pluginId
            displayName = 'Gradle plugin for running tests against multiple databases'
            description = 'Gradle plugin for running tests against multiple databases'
        }
    }
}

idea {
    project {
        languageLevel = '1.8'
    }
    module {
        downloadSources = true
        downloadJavadoc = false
    }
}

ext {
    // look for command-line overrides of the username/password pairs for publishing
    if ( project.hasProperty( 'hibernatePluginPortalUsername' ) ) {
        if ( ! project.hasProperty( 'hibernatePluginPortalPassword' ) ) {
            throw new GradleException( "Should specify both `hibernatePluginPortalUsername` and `hibernatePluginPortalPassword` as project properties" );
        }
        credentials.hibernatePluginPortalUsername = project.property( 'hibernatePluginPortalUsername' )
        credentials.hibernatePluginPortalPassword = project.property( 'hibernatePluginPortalPassword' )
    }
    else if ( System.properties.hibernatePluginPortalUsername != null ) {
        if ( System.properties.hibernatePluginPortalPassword == null ) {
            throw new GradleException( "Should specify both `hibernatePluginPortalUsername` and `hibernatePluginPortalPassword` as system properties" );
        }
        credentials.hibernatePluginPortalUsername = System.properties.hibernatePluginPortalUsername
        credentials.hibernatePluginPortalPassword = System.properties.hibernatePluginPortalPassword
    }
}

gradle.taskGraph.whenReady { tg ->
    if ( tg.hasTask( project.tasks.publishPlugins ) ) {
        if ( credentials.hibernatePluginPortalUsername == null ) {
            throw new RuntimeException( "`hibernatePluginPortalUsername` not found" )
        }
        if ( credentials.hibernatePluginPortalPassword == null ) {
            throw new RuntimeException( "`hibernatePluginPortalPassword` not found" )
        }
    }
}

wrapper {
    // To upgrade the version of gradle used in the wrapper, run:
    //     ./gradlew wrapper --gradle-version NEW_VERSION
    distributionType = Wrapper.DistributionType.ALL
}